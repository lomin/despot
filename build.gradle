apply plugin: 'idea'
apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'

group      = 'de.itagile'
archivesBaseName = "despot"
version    = '0.0.1'

sourceCompatibility = 1.7
targetCompatibility = 1.7

[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

repositories {
    mavenCentral()
}

dependencies {
    compile "com.sun.jersey:jersey-core:1.17.1"
    compile "com.sun.jersey:jersey-client:1.17.1"
    compile "com.googlecode.json-simple:json-simple:1.1.1"
    testCompile group: 'org.mockito', name: 'mockito-all', version: '1.9.5'
    testCompile group: 'junit', name: 'junit', version: '4.+'
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.0'
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

signing {
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name 'despot'
                packaging 'jar'
                // optionally artifactId can be defined here
                description 'Despot ensures that REST-interfaces satisfy a formal specification.'
                url 'https://github.com/lomin/despot'

                scm {
                    connection 'scm:git:git@github.com:lomin/despot.git'
                    developerConnection 'scm:git:git@github.com:lomin/despot.git'
                    url 'git@github.com:lomin/despot.git'
                }

                licenses {
                    license {
                        name 'The Apache License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id 'lomin'
                        name 'Steven Collins'
                        email 'steven.collins@it-agile.de'
                    }
                }
            }
        }
    }
}